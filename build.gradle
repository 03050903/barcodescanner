buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:0.8.+'
    }
}

allprojects {
    apply plugin: 'maven'
    apply plugin: 'signing'

    group = 'me.dm7.barcodescanner'
    version = '1.0'

    repositories {
        mavenCentral()
        maven {
            url 'https://oss.sonatype.org/content/groups/public'
        }
    }

    signing {
        required { has("release") && gradle.taskGraph.hasTask("uploadArchives") }
        sign configurations.archives
    }

    uploadArchives {
        repositories.mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: sonatypeRepo) {
                authentication(userName: sonatypeUsername , password: sonatypePassword)
                releases(updatePolicy: 'always')
            }

            snapshotRepository(url: sonatypeSnapshotRepo) {
                authentication(userName: sonatypeUsername , password: sonatypePassword)
                snapshots(updatePolicy: 'always')
            }

            pom.project {
                url 'https://github.com/dm77/barcodescanner'

                scm {
                    url 'scm:git@github.com:dm77/barcodescanner.git'
                    connection 'scm:git@github.com:dm77/barcodescanner.git'
                    developerConnection 'scm:git@github.com:dm77/barcodescanner.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'dm77'
                        name 'Dushyanth Maguluru'
                        email 'dushyanth.maguluru@gmail.com'
                    }
                }
            }
        }
    }

    ext {
        supportLibraryDependency = 'com.android.support:support-v4:19.0.1'
    }
}

subprojects {
    afterEvaluate {
        android {
            compileSdkVersion 19
            buildToolsVersion '19.0.1'

            defaultConfig {
                minSdkVersion 8
                targetSdkVersion 19
                versionCode 1
                versionName "1.0"
            }
        }
    }
}

uploadArchives {
    repositories.mavenDeployer {
        pom.project {
            name 'Barcode Scanner Parent'
            artifactId "parent"
            description 'A collection of barcode scanner integration libraries for Android'
        }
    }
}